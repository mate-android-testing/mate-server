buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id "com.github.spotbugs" version "4.0.2"
}

apply plugin: 'checkstyle'
apply plugin: 'pmd'

group 'org.mate'
version '0.4'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'org.mate.Server'
    }
    // archiveBaseName = rootProject.name + '-all'
    // archiveFileName = rootProject.name + '.jar'
    archiveName(rootProject.name + '.jar')
    from {configurations.compile.collect{it.isDirectory() ? it : zipTree(it)}}
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')
    compile group: 'org.jacoco', name: 'org.jacoco.report', version: '0.8.5'
    testImplementation 'junit:junit:4.12'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-text
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
}

test {
    useJUnit()

    maxHeapSize = '1G'
}

spotbugs {
    toolVersion = '4.0.0'
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
}

checkstyle {
    toolVersion = '8.20'
}

pmd {
    ignoreFailures true
}

spotbugsMain {
    reports {
        xml.enabled = false
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

task checkstyle(type: Checkstyle) {
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    //configProperties.checkstyleSuppressionsPath = file("$configPath/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    reports {
        html.enabled = true
        xml.enabled = false
        html.destination file("$project.buildDir/reports/checkstyle/checkstyle.html")
    }

    classpath = files()
}
